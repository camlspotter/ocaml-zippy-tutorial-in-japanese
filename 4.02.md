OCaml 4.02 の予習
=================================

printf が GADT で強化された
-----------------------------

まあ簡単に言うと毎回プリントする再にインタープリットしていた format 文字列を
もすこし代数的なデータに落して嬉しいみたいな感じ、なのだが、

    (* ocaml -dlambda *)
    # Printf.sprintf "hello %s !!" "world";;
    (apply (field 3 (global Printf!))
      [0: [11: "hello " [2: 0a [11: " !!" 0a]]] "hello %s !!"] "world")
    - : string = "hello world !!"

おお、 `"hello %s !!"` が `"hello "` と `" !!"` とその間の何かにコンパイル時に分解されている。
だから毎回インタープリットしないから速い。速いとすごい、だから強いってことか。
ようやく P4 で同じことをやっていた O'Rakuda の `<:qq<fmt>>` みたくなったということですね。

この辺なんか GADT 使って頑張ったーみたいなことが書いてあるんだけど、
結局相変わらずコンパイラ側の format 文字列型付けハックは残っているのでチートであることは変らない。
まあ、使う人には、中身はほぼ無意味ですね。
興味があったら Pervasives.CamlinternalFormatBasics と CamlinternalFormat を読めということらしい。

しかし、こんな変更ガガっと入れるのはええけど、ちゃんとテストしてあるんか？
これは不安ですね…

module alias が軽くなった
---------------------------

中の人がぼんやりしてたら、外の人がモジュールをごにょごにょ操作するしまくる
OCaml プログラミングスタイルというのを確立してしまった感がある。特に Jane Street 
のコードや俺のコードはモジュールをあっちこっちにエイリアスしたりインクルードしたり
大変。それはそれで便利なんだが、実は重かった。例えば、次のような stdlib のモジュールを
何も考えずに全部エイリアスするコード、

    module Arg = Arg
    module Array = Array
    module ArrayLabels = ArrayLabels
    module Buffer = Buffer
    module Callback = Callback
    module Char = Char
    module Complex = Complex
    module Digest = Digest
    module Filename = Filename
    module Format = Format
    module Gc = Gc
    module Genlex = Genlex
    module Hashtbl = Hashtbl
    module Int32 = Int32
    module Int64 = Int64
    module Lazy = Lazy
    module Lexing = Lexing
    module List = List
    module ListLabels = ListLabels
    module Map = Map
    module Marshal = Marshal
    module MoreLabels = MoreLabels
    module Nativeint = Nativeint
    module Obj = Obj
    module Oo = Oo
    module Parsing = Parsing
    module Pervasives = Pervasives
    module Printexc = Printexc
    module Printf = Printf
    module Queue = Queue
    module Random = Random
    module Scanf = Scanf
    module Set = Set
    module Sort = Sort
    module Stack = Stack
    module StdLabels = StdLabels
    module Stream = Stream
    module String = String
    module StringLabels = StringLabels
    module Sys = Sys
    module Weak = Weak

これを 4.01.0 でコンパイルすると結果はこんなサイズ:

    -rwxr-xr-x 1 161780 m.cmi
    -rwxr-xr-x 1   2486 m.cmo
    -rwxr-xr-x 1  68919 m.cmx
    -rw-r--r-- 1    943 m.ml
    -rw-r--r-- 1   3100 m.o

で、これがなんか 4.02.0 では軽くなったんだと:

    -rwxr-xr-x 1 2571 m.cmi <---- !!!
    -rwxr-xr-x 1 2405 m.cmo
    -rwxr-xr-x 1 2499 m.cmx
    -rw-r--r-- 1  943 m.ml
    -rw-r--r-- 1 1521 m.o   <---- !!!

おお、はじめは `.cmo` だけ見てたので意義が判らなかったが、 signature (`.cmi`)
と native code (`.cmx` と `.o`) は凄く小くなっている。どうも `strings` で
`.cmi` や `.cmx` を見ると、4.01.0 以前では `module M = N` の `N` の中身を
真面目に展開していた、その展開を止めてエイリアスにした、そういうことらしい。

string は immutable の方向へ
--------------------------------

string の他に、 bytes という型が入った。通常は string と bytes は同じ型で単一化できる:

    let x : bytes = "hello"
    let y : string = "world"
    let xy = [ x; y ]   (* 問題ない *)

だが、`-safe-strings` というコマンドラインスイッチを入れるとこの同一視が無くなり、
string は immutable な文字列、bytes は mutable な文字列という扱いに変る。
`"hello"` は string としてしか解釈されなくなるので、上の `let x : bytes = "hello"` の
ところでエラーになる。bytes を作るには、

    let x = Bytes.of_string "hello"

と書かなければならなくなる。

例によって string はただの char 配列でしかないので UTF-8 とかを扱いたい時はライブラリを
通す必要がある。

実装上は実に簡単なのでバグとかは無いと思う。

新しい文字列リテラル
----------------------

`{id|文字列|id}` という新しい文字列が書けるようになった。例えば `{|abcd"\/|}` と書くと、
`"abcd\"\\/"` という文字列と見做される。 `{|..|}` の中では `\` は特殊文字と見做されないので
`\` を入れる為に `\\` と重ねなくてよいのだ。これで `\` が瀕出する PCRE も楽に書けるわけ。 
id つきのは `{special|文字列|special}` などと書く。ちゃんと対になっていけないといけない。
冗長でちょっとウザと思うのだが、文字列の中に `|}` を書きたい場合はこれを使うのだと思われる。
後多分 id は attribute として使えるはず。

これも簡単な部類。ガンガン使っていけるはず。

Generative functors
--------------------------

今までの OCaml の functor は applicative functor と呼ばれる:

    module Make(A : sig end) = struct
      type t = Foo
      let x = Foo      
    end

    module Empty = struct end

    module A = Make(Empty)
    module B = Make(Empty)

    let () = assert (A.x = B.x)

が通る。つまり A.t と B.t は同一視される。キモい、と思われるかもしれないが、

    let _ : A.t = MA1.x

ではなく

    let _ : M(Empty).t = MA1.x

と書けたりするため、役に立たないわけではない。

でも、キモい、 `A.t` と `B.t` は区別されるべき、という場合もあり、そういう functor
は generative functor と言うそうな。まあ一回一回型が新しく生成されるわけだから generative なのかな。
それが書けるようになった。

    module A = Make()
    module B = Make()

と、`(Empty)` 代りに `()` と書くと `A.t` と `B.t` は別の型になって `A.x = B.x` は型エラーになる。
`()` は空の structure にしか使えないので、非空の引数を取る functor に対して generative functor
は作れないかというと、そういうことはなく、単にもう一つ `()` を引数に取る functor を作ってやれば
よろしい。

    module Make2(A : sig type t end)() = struct
      type t = Foo
      ...
    end

Applicative と generative の二つの functor が欲しいのは判るのだが、
解決が cryptic で駄目なんじゃないかとおもう。Backward compatibility は大事だし新キーワード
恐怖症も理解できるけど、もうなんか限界が来ているじゃないかと思わせる変更。


Attributes and extension nodes
------------------------------------

Attribute ってのは簡単に言うと OCaml のコードの好きな所に好きなことを書けるのである。
ほいでそれを好きにパースして利用するわけだ。パースされた AST を貴方がプログラムで好きに変換して
OCaml コンパイラに戻す。変換次第でバニラ OCaml では出来無かった機能を追加できるわけ。 

元々 OCaml でこういう機能追加は CamlP4 っていうかっちょいいプリプロセッサでやっていたんだけれども、
これには幾つか問題があった:

* 難しい。とにかく複雑。ドキュメント無いし
* パーステクノロジが違う。OCaml バニラは lex+yacc だけど P4 はストリームパーサー。ただし文法拡張ができる。
* 文法拡張するので拡張部分の事を知らないプログラムでは拡張を使ったコードを機械的に読み込めない。
* プリプロセスの手間がかかりどうしても遅い

で、この attributes では、もう文法拡張は止めましょう。その代り、 AST の好きなところにマークを
付けれるようにしましょう。で、そのマークを元に AST を変更するフィルタを書きましょう、
ということになった。

これで嬉しいのは、 attributes を知らなくても少なくともコードのパースはできるってことで、
エディタでのインデントとかそういう機能は P4 の文法拡張のことを気にしなくて良くなったってこと。

Attribute は `[@...]` というキモい文法でコード中に埋め込むことができる。
問題は、 `[@...]` だけでなく `[@@...]` や `[@@@...]` とかあることで醜いですねー。
これは AST のどこに attribute がくっついているかを示すものらしい:

* `[@...]` は expression とか
* `[@@...]` は structure や signature item レベル
* `[@@@...]` は多分、ソースコードファイル全体 (compilation unit) レベル

覚え方は `@`(at) は ATtribute の頭文字。 

例えば

    let x = 1 [@attr]

は `1` に対して `attr` という attribute がくっついているが、

    let x = 1 [@@attr]

は `let x = 1` という structure item に対して `attr` という attribute がくっついている。

でこの attribute なんだけど、多分、メジャーな CamlP4 拡張は殆どこれでカバー「できる」ということになっている。だから P4 はさようならになると思われる。

既にこの attribute は OCaml コンパイラ内部でも使われていて、`obj.mli` では

    val final_tag : int [@@ocaml.deprecated]

とか書かれている。 `final_tag` を使うと `ocamlc` が警告を出してくれる。

あ、あと、 `[%...]` と `[%%...]` という extension てのもあるんだ。
これは attribute と似ているんだけど、どっちかというと構文のキーワードを修飾して
特殊な意味を持たせるのに使う。

    [%io
      let x = read in 
      print x  
    ]

とか書いて、これを適切なパーサに通すと

    read >>= fun x -> print x

とかに変えてくれる、そんな感じ。で、 extension には簡単な書き方が用意されていて、
上のはこう書いてもよい:

    let%io x = read in
    print x

うーーーーーん、どうなんだろうねえ。これで Haskell の `do` 記法書かせて見栄えどうなるんかね。 
実際、 lwt ライブラリの `bind` である `lwt x = ... in` は

    let%lwt x = ... in

と書かせたいみたいだけど、醜いよね。普通の `let` と `let%lwt` が混在してだらららーっ
と並んでたら結構鬱になる気がするが…

[OMonad](https://github.com/danmey/omonad) というので pa_monad っぽいの書けるのねえ:

    let vs = perform (
      [|x; y|] <-- [[|1; 2|]; [||]; [|3; 4; 5|]; [|6; 7|]];
      return (x + y)
    ) in

んー？ `[|x; y|] <-- [|...|]` ってことは、 `<--` は普通の二項演算子、
てことはこの左辺 `[|x; y|]` は pattern の様に見えて、実は AST 上では expression なのか…
filter で expression から pattern に変換しているのだな… OMonad のコードでも確認した。
これはキモい。

うーん、 OCaml バニラ構文に閉じ籠るのはいいんだけど、じゃあバニラ構文今で足りてるのかというと、
ということで、 P4 無くなったのはいいけど P4 の柔軟性無くなったのはキツいんじゃないですか。

Open extensible types
-----------------------------

まあコンストラクタが増やせる variant のようなもの。

    type t = ..

で宣言する。この `..` があるので普通の variant とは区別される。コンストラクタを足すには

    type t += A

とか

    type t += B of int | C

とか書く。このコンストラクタの追加は別のモジュールで行われてもよいし、お互いの追加は
お互いの存在を知っている必要もない。コンストラクタ名が被っていても構わない。

この辺はキモいかもしれないが、例外の一般化と思う方が理解しやすいかもしれない。
例外の型 exn は次の open extensible type と同じ

    type exn = ..

で、 `exception E of t` は

    type exn += E of t

と同じ。実際に exn は open extensible type と内部では同じになっている。
これを見れば、例外宣言はお互いの例外宣言の存在を知っている必要はないし、
例外名が被っていても構わない(`exception E;; exception E of int` とか普通やらないけど)
ことから理解できるだろう。

で実際の挙動はこうなっている

    type t = ..
    type t += A of int
    type t += B of int
    
    open Obj
    
    let parse_tag t = 
      if t = int_tag then `int
      else if t = string_tag then `string
      else if t = lazy_tag then `lazy_
      else if t = closure_tag then `closure
      else if t = object_tag then `object_
      else if t = infix_tag then `infix
      else if t = forward_tag then `forward
      else if t = no_scan_tag then `noscan
      else if t = abstract_tag then `abstract
      else if t = custom_tag then `custom
      else if t = final_tag then `final
      else if t = out_of_heap_tag then `out_of_heap
      else if t = unaligned_tag then `unaligned
      else `unknown t
    
    let tag_name = function
      | `int -> "int"
      | `string -> "string"
      | `lazy_ -> "lazy"
      | `closure -> "closure"
      | `object_ -> "object"
      | `infix -> "infix"
      | `forward -> "forward"
      | `noscan -> "noscan"
      | `abstract -> "abstract"
      | `custom -> "custom"
      | `final -> "final"
      | `out_of_heap -> "out_of_heap"
      | `unaligned -> "unaligned"
      | `unknown x -> string_of_int x 
    
    
    let dump o = 
      let open Format in
      let rec dump ppf o =
        let t = parse_tag @@ tag o in
        match t with
        | `int -> fprintf ppf "%d" @@ obj o
        | `double -> fprintf ppf "%.4f" @@ obj o
        | `string -> fprintf ppf "%S" @@ obj o
        | _ -> 
           fprintf ppf "[%s @[" @@ tag_name t;
           let s = size o in
           for i = 0 to s - 1 do
             dump ppf (field o i);
             fprintf ppf "@ "
           done;
           fprintf ppf "@]]"
      in
      eprintf "%a@." dump o
    
    let () = 
      let o = repr (B 3) in
      dump o

このプログラムの出力により `B 3` は

    [0 [object "B" 86 ] 3 ]

というデータになっていることがわかる。つまり、第一要素に何かコンストラクタ名を含む
オブジェクトタグのついたブロックを含む組だ。普通のバリアントと違ってタグは整数ではなくて
このオブジェクトタグのついたブロックを使う。別モジュールでやはり `B` というコンストラクタを
足した場合でもこの `[object "B" 86 ]` タグが作られるが、この二つのタグを区別できるように
構造比較ではなくてアドレスを判別に使う。これは今までの例外の実装と同じだね。
だから output_value や Marshal でデータを書き出し読み込んだ場合もとのコンストラクタとは
同一視できなくなる。これも例外と同じ。

4.01 では例外には object tag は使われていなかったんだけど、これはどういうことなんかいな。
例えば上の関数で Failure "hello" はこうなってたんだけど:

    [0 [0 "Failure" ] "hello" ]

object に付いてくる hash で何か高速化狙ってるのかな? ちょっと不安を感じる…

例外キャッチを match with に埋め込めるようになった
-----------------------------------------------

ただの糖衣構文。キモい。

    let f lst = 
      try 
        match List.assoc "x" lst with
        | None -> 0
        | Some x -> x
      with
      | Not_found -> 0

を

    let f lst = 
      match List.assoc "x" lst with
      | None -> 0
      | Some x -> x
      | exception Not_found -> 0

と書けるようになった。そこまでして短かく書きたければ、
そもそも例外を発生させないライブラリ作りをするべきでは。キモい。

まあ単なる糖衣のはずなので使って問題ないはず。

LablTk が本体から消える
----------------------------

まあ、これ私が大分遊んだやつなんだけど、今更 Tcl/Tk とかねぇ。GUI は外に出しましょう！

Camlp4 が本体から消える
------------------------------

これは p4 が終わりということではなくて、別のレポジトリで管理されるということなんだけれども、
もちろん、 p4 は終わりという意味です。
